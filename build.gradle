
plugins {
    id 'org.gradle.maven-publish'
    id 'org.gradle.jacoco'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'org.sonarqube' version '2.8'
}

group = 'com.github.rodm'
version = '0.7-SNAPSHOT'

ext {
    def urlName = "${project.version}".endsWith('-SNAPSHOT') ? 'repository.snapshots.url' : 'repository.releases.url'
    repositoryUrl = findProperty(urlName) ?: ''
    repositoryUsername = findProperty('repository.username') ?: ''
    repositoryPassword = findProperty('repository.password') ?: ''
    serverUrl = project.findProperty('teamcity.server.url') ?: 'http://localhost:8111/app/dsl-plugins-repository'
}

repositories {
    jcenter()
    maven {
        url 'https://download.jetbrains.com/teamcity-repository'
    }
    maven {
        url "${serverUrl}"
        allowInsecureProtocol = true
    }
}

configurations {
    testImplementation.extendsFrom compileOnly
}

dependencies {
    compileOnly group: 'org.jetbrains.teamcity', name: 'configs-dsl-kotlin-plugins', version: '1.0-SNAPSHOT'
    compileOnly group: 'org.jetbrains.teamcity', name: 'configs-dsl-kotlin'
    compileOnly group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8'
    compileOnly group: 'org.jetbrains.kotlin', name: 'kotlin-script-runtime'

    testImplementation platform(group: 'org.junit', name: 'junit-bom', version: '5.6.2')
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: '2.2'

    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
}

java {
    withJavadocJar()
    withSourcesJar()
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
}

publishing {
    repositories {
        maven {
            url repositoryUrl

            credentials {
                username repositoryUsername
                password repositoryPassword
            }
        }
    }

    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = 'TeamCity DSL Extension Library'
                description = 'A  library that extends the TeamCity Kotlin DSL '
                url = 'https://github.com/rodm/teamcity-dsl-extensions'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'rodm'
                        name = 'Rod MacKenzie'
                        email = 'rod.n.mackenzie@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/rodm/teamcity-dsl-extensions.git'
                    developerConnection = 'scm:git:ssh://github.com/rodm/teamcity-dsl-extensions.git'
                    url = 'https://github.com/rodm/teamcity-dsl-extensions'
                }
            }
        }
    }
}
